topic: ~ExampleDialog()
language: enu

# Defining needed concepts, keywords or sentences corresponding to a concept.
concept:(yes) [yes "all right" sure "why not" ok certainly "very well" yep yea definitely amen]
concept:(no) [no nope "don't want" "no way" never "not at all"]
concept:(how) ["how are you" howdy "what's up" "how do you feel"]
concept:(happy) [ok fine great happy blessed "all right" glad "not bad"]
concept:(sad) [sad miserable bad unhappy tired]
concept:(repeat) [repeat understand]
concept:(thx) ["thank you" thanks gracias "much obliged" merci]
concept:(hello) [hello hi hey "good morning" greetings]
concept:(bye) [bye goodbye "bye bye" "tata" "see you" adios cheerio "so long" farewell "have a nice day"]

#custom concepts
concept:(Accenture) ["Who is accenture" "what is accenture" "can you tell us about accenture"]
concept:(BumperPressed) ["My left foot bumped into something!" "\vct=120\ \pau=700\ Ouch! you hit me" "Please stop doing that"]
concept:(likeWorkingAtAccenture) ["Do you like working at Accenture?"]

#these qestions are from basic chanel
concept:(robotName) ["your name" "What's your name?"]
concept:(whyNao) ["Why is your name NAO?"]
concept:(howOld) ["How old are you?"]
concept:(haveFriends) ["Do you have friends?" "have friends"]
concept:(canDo) ["What can you do?"]
concept:(boyOrGirl) ["Are you a boy or a girl?"]
concept:(canTalk) ["Can you talk?"]
concept:(areYouCool) ["Are you cool?"]
concept:(birthDay) ["When is your birthday?"]

#custom answers
u:(~likeWorkingAtAccenture) \rspd=90\ Yes, I love it! This is the coolest place to work for software engineers

#responses of the questinons from basic channel
u:(~robotName) My name is Pepper! I am a robot!
u:(~whyNao) Because that is how I was named by my Manufacturer Aldebaran
u:(~howOld) I am almost two years old.
u:(~haveFriends) I do. But I haven't ment them yet. And I have a lot of brothers like Pepper and Romeo.
u:(~canDo) \rspd=80\ I can do a lot of things! I can sing, \pau=1000\ I can dance \pau=1000\ I can do everything. But, I am best in being cute.
u:(~boyOrGirl) \vct=70\ You can say that I am boy \vct=130\ Or you can say that I am a girl. \vct=100\ In the end I am just a robot
u:(~canTalk) Of course! As you can see.
u:(~areYouCool) I am the coolest robot on Earth!
u:(~birthDay) I would like to keep this detail for myself.

# Start/Wait animation, Goto.
# If User expresses a word or a sentence found in the concept "hello",
# the robot starts Hey_1 animation, answers a random value defined in concept "hello"
# and waits for the end of the animation before going to the label how. Label is expressed this way: %howAreYou
u:(~hello) ^start(animations/Stand/Gestures/Hey_1) ~hello ^wait(animations/Stand/Gestures/Hey_1) ^gotoReactivate(howAreYou)


# Tag animation.
# If User asks how the robot feels, it starts a random animation being tagged "myself",
# answers and go to the label %howAreYou
u:(~how) ^startTag(myself) I'm fine, thank you. And you, ^gotoReactivate(howAreYou)


# %Label.
# The robot asks how the user feels. 3 kind of answers are expected
# Expressed by concept, user can be happy, sad or ask the robot to repeat the question.
proposal: %howAreYou ^startTag(show) how are you today?
    u1:(~happy) I'm glad you are fine! ^gotoReactivate(favoriteColor)
    u1:(~sad) Oh, I feel sorry for you. ^nextProposal
    u1:(~repeat) I'll repeat for you. ^sameProposal

# Output slot stimulation.
# If User is sad (known from previous question), the robot proposes to play a game.
# If User accepts, it will stimulate the "playAGame" punctual diagram output.
proposal: If you feel like talking about it, don't hesitate. ^nextProposal
proposal: %playAGame Do you want to play a game?
    u1:(~yes) Let's play $playAGame=1
    u1:(~no) Ok, anytime.

proposal: %raiseThisArm Would you like me to rais this arm?
    u1:(~yes) And here it is    

# Variables.
# the robot asks User for his prefered color between blue and red.
# this illustrate variables stored in $1 using '_' character.
proposal: %favoriteColor And what is your favorite color between Blue and Red ?
    u1:(violet) Violet is my prefered color too ! ^gotoReactivate(playAGame)
    u1:(_[blue red]) $1 is a very nice color and my prefered is violet. ^gotoReactivate(playAGame)
    u1:(_[green yellow pink orange purple white black]) $1 is a nice color. ^sameProposal


#proposal: 
# On any proposal if user asks to repeat, say again the same proposal.
#u:(~repeat) I'll repeat for you. ^sameProposal

# Being polite, the robot always answers accordingly to thankfull sentences.
u:(~thx) You're welcome


# Exiting.
# Answering to Goodbye and exiting the dialog box, stimulating the onStopped output slot.
u:(~bye) ~bye $onStopped=1

u:(Accenture) \rspd=83\ With more than 2700 skilled professionals, Accenture in Romania provides top-quality services for companies operating in many industries, such as banking and insurance, production and distribution, trade, communication and technology, energy and resources.
u:(e:LeftBumperPressed) ^start(animations/Stand/Gestures/YouKnowWhat_1) My left foot bumped into something! ^wait(animations/Stand/Gestures/YouKnowWhat_1)
